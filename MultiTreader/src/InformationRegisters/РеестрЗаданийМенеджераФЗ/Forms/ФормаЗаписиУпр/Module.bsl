
&НаКлиенте
Перем ТекИдентификаторМенеджера;


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
		
	Если ЗначениеЗаполнено(Параметры.ЗначениеКопирования) Тогда
		Запись.КлючНастроек = Неопределено;
		Запись.ПериодРегистрации = Неопределено;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Запись.ПериодРегистрации) Тогда
		Запись.ПериодРегистрации = ТекущаяДата();
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	НастройкиМенеджера = ТекущийОбъект.НастройкиЗадания.Получить();
	
	ЕстьНастройки = ?(НастройкиМенеджера = Неопределено, Ложь, Истина);
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ТекущийОбъект.НастройкиЗадания = Новый ХранилищеЗначения(НастройкиМенеджера);
	
	Если НЕ ЗначениеЗаполнено(ТекущийОбъект.КлючНастроек) Тогда
		ТекущийОбъект.КлючНастроек = Новый УникальныйИдентификатор();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОбновитьДекорацииНастроек();
	
	ТекИдентификаторМенеджера = Запись.ИдентификаторМенеджера;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ккЗаполнениеНастроекЗаданияМенеджера" Тогда
		
		Если ТипЗнч(Параметр) = Тип("Структура") Тогда
			
			НастройкиМенеджера = Параметр;
			ЕстьНастройки = Истина;
			ОбновитьДекорацииНастроек();
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьОткрытьНастройки(Команда)
	
	Если Не ЗначениеЗаполнено(Запись.ИдентификаторМенеджера) Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыФайла = ПолучитьВнешнююОбработкуПоИдентификатору(Запись.ИдентификаторМенеджера);
	
	Если ПараметрыФайла = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(ПараметрыФайла.ДвоичныеДанные) <> Тип("ДвоичныеДанные") Тогда
		
		ТекстСообщения = НСтр("ru ='Для данного идентификатора не указана внешняя обработка настроек.'");
		Сообщить(ТекстСообщения);
		Возврат;

	КонецЕсли;
	
	ПолноеИмяФайла = КаталогВременныхФайлов() + ПараметрыФайла.ИмяФайлаОбработки;
	ПараметрыФайла.ДвоичныеДанные.Записать(ПолноеИмяФайла);
	
	АдресХранилища = "";
	Результат = ПоместитьФайл(АдресХранилища, ПолноеИмяФайла, , Ложь);
	ИмяОбработки = ПодключитьВнешнююОбработку(АдресХранилища);
	
	Если ТипЗнч(НастройкиМенеджера) <> Тип("Структура") Тогда
		НастройкиМенеджера = Новый Структура;
	КонецЕсли;
		
	Попытка
		ПутьКФорме = СтрШаблон("ВнешняяОбработка.%1.Форма", ИмяОбработки);
		ПараметрыФормы = Новый Структура("НастройкиМенеджера", НастройкиМенеджера);
		ОткрытьФорму(ПутьКФорме, ПараметрыФормы, ЭтаФорма, УникальныйИдентификатор, , , , РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	Исключение
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Ошибка: " + ОписаниеОшибки();
		Сообщение.Сообщить();
	КонецПопытки;
	
	УдалитьФайлы(ПолноеИмяФайла);

КонецПроцедуры

&НаКлиенте
Процедура ИдентификаторМенеджераПриИзменении(Элемент)
	
	Если ТекИдентификаторМенеджера <> Запись.ИдентификаторМенеджера Тогда
		
		НастройкиМенеджера = Неопределено;
		ЕстьНастройки = Ложь;
		ТекИдентификаторМенеджера = Запись.ИдентификаторМенеджера;
		ОбновитьДекорацииНастроек();
		
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервере
Функция ПодключитьВнешнююОбработку(АдресХранилища)
	ОписаниеЗащиты = Новый ОписаниеЗащитыОтОпасныхДействий;
	ОписаниеЗащиты.ПредупреждатьОбОпасныхДействиях = Ложь;
	Возврат ВнешниеОбработки.Подключить(АдресХранилища, ,Ложь, ОписаниеЗащиты);
КонецФункции

&НаКлиенте
Процедура ОбновитьДекорацииНастроек()
	
	Если ЕстьНастройки Тогда
		Элементы.НадписьПредупреждение.Заголовок = НСтр("ru ='Настройки заполнены'");
		Элементы.ЗаполнитьНастройки.Заголовок    = НСтр("ru ='Открыть'");
	Иначе
		Элементы.НадписьПредупреждение.Заголовок = НСтр("ru ='Настройки не заполнены'");
		Элементы.ЗаполнитьНастройки.Заголовок    = НСтр("ru ='Заполнить'");
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьВнешнююОбработкуПоИдентификатору(Идентификатор)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ккИдентификаторыМенеджераПотоков.Хранилище,
	|	ккИдентификаторыМенеджераПотоков.ИмяФайлаОбработки
	|ИЗ
	|	Справочник.ИдентификаторыМенеджераПотоков КАК ккИдентификаторыМенеджераПотоков
	|ГДЕ
	|	ккИдентификаторыМенеджераПотоков.Ссылка = &Идентификатор";

	Запрос.УстановитьПараметр("Идентификатор", Идентификатор);

	РезультатЗапроса = Запрос.Выполнить().Выбрать();

	Если РезультатЗапроса.Следующий() Тогда
		
		ПараметрыФайла = Новый Структура;
		ПараметрыФайла.Вставить("ИмяФайлаОбработки", РезультатЗапроса.ИмяФайлаОбработки);
		ПараметрыФайла.Вставить("ДвоичныеДанные"   , РезультатЗапроса.Хранилище.Получить());
		
		Возврат ПараметрыФайла;
		
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции





